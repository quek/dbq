(in-package :dbq)

(defgeneric pluralize (x)
  (:method (x)
    (pluralize (string-downcase x))))

(defmethod pluralize ((string string))
  (let* (flush add
               (len (length string))
               (last-char-raw (char string (1- len)))
               (last-char (char-upcase last-char-raw))
               (penult-char (char-upcase (if (> len 1)
                                             (char string (- len 2))
                                             #\Nul))) ;dummy
               (last-3 (subseq string (max 0 (- len 3)))))
    (declare (character last-char-raw last-char penult-char)
             (string last-3))
    (setf (values flush add)
          (cond ((and (char-equal last-char #\Y)
                      (not (member penult-char '(#\A #\E #\I #\O #\U))))
                 (values 1 "ies"))
                ((or (string-equal string "ox")
                     (string-equal string "vax"))
                 (values nil "en"))
                ((or (and (char= last-char #\H)
                          (member penult-char '(#\C #\S)))
                     (member last-char '(#\S #\Z #\X)))
                 (values nil "es"))
                ((string-equal last-3 "man")
                 (values 2 "en"))
                ((string-equal last-3 "ife")
                 (values  2 "ves"))
                (t (values nil "s"))))
    (when flush
      (setq string (subseq string 0 (- len flush))))
    (concatenate 'string string add)))

(defgeneric singularize (x)
  (:method (x)
    (singularize (string-downcase x)))
  (:method ((string string))
    (cond ((alexandria:ends-with-subseq "ies" string)
           (ppcre:regex-replace "ies$" string "y"))
          ((alexandria:ends-with-subseq "IES" string)
           (ppcre:regex-replace "IES$" string "Y"))
          ((or (alexandria:ends-with #\s string)
               (alexandria:ends-with #\S string))
           (subseq string 0 (1- (length string))))
          (t
           string))))

(defun sym (&rest args)
  (intern
   (with-output-to-string (out)
     (loop for x in args
           do (write-string (string-upcase x) out)))
   *package*))

(defun key-sym (&rest args)
  (let ((*package* (find-package :keyword)))
    (apply #'sym args)))

(defun str (&rest args)
  (format nil "狺狎珞┅ㄤ彐躅泔祯眍钺礤麸箪雉钺礤ㄣ镬蹴瞽钺礤簌篚怏糸趱翦＼＼泔祯眍钺礤┅ㄤ彐躅泔祯眍钺礤麸徙沐篌矧ㄣ镬蹴瞽钺礤簌ㄣ镬蹴瞽钺礤麸箪雉钺礤泔祯眍钺礤掀┅ㄤ彐躅孱篚蝈扉篝ㄩ扉篝扉篝┅ㄤ彐躅沆狍蟓麸翎忪瀛钺礤ㄣ灬篌篚怏糸趱翦＼＼痨躜犰辁篝蜷铉滹黝汜箦ㄣ灬篌钺礤沆狍螬┅┅ㄤ彐珏铄蜷麸翎忪瀛钺礤ê礤翳镤麸泔祯眍钺礤┅ê礤翳镤è簌礅镬┅麸翎忪瀛钺礤ㄦ轭洵沆狍┅ê礤翳镤è篝犷溽蜾沆狍螬麸翎忪瀛钺礤ㄣ灬篌麸翎忪瀛钺礤┅ê礤翳镤è篝犷溽蜾镡赍泗┅麸翎忪瀛钺礤ㄣ灬篌镦┅┅ㄤ彐珏铄蜷麸泔祯眍钺礤ê礤翳镤è篝蜷铉┅鏖翳秕麴豸麸篝蜷铉秕舂黩轸瀛汨狎＼秕舂祜镳骘徙蝻篌殒ㄣ栳蚪＼喋滹黩轸瀛汨狎＼秕舂滹黩轸瀛汨狎秕舂黩轸瀛汨狎＼秕舂┅ê礤翳镤è簌礅镬┅麸泔祯眍钺礤篚怏糸趱翦＼＼篝蜷铉滹黝汜箦┅┅ㄤ彐珏铄蜷麸篑飙鲠祯ê礤翳镤è铛礅弪┅痱轭悱麸篝蜷铉┅ê礤翳镤è篝蜷铉┅鏖翳秕麴豸麸篝蜷铉秕舂黩轸瀛汨狎＼秕舂祜镳骘徙蝻篌殒ㄣ栳蚪＼悌滹黩轸瀛篝蜷铉Ё秕舂屐箦滹黩轸瀛汨狎秕舂黩轸瀛汨狎＼秕舂┅ê礤翳镤è祜汜飙糸礤呼轫弩翎眇┅祜汜飙糸礤烘矧磲舡糸礤篝蜷铉铋烘矧磲Ж＼葫遽＼喉镱翳＼轰狴＼箴徙鸿秕＼喉轭＼后邈＼З┅ㄤ彐珏铄蜷麸扉箴鲠祯鲠祯泔祯眍豉疱ê礤翳镤鲠祯泔祯眍豉疱鲠祯濠ê礤翳镤鲠祯ㄣ镬蹴瞽豉疱ㄥ耢轰狒弭轫濠┅祜汜飙糸礤乎铋鲥蝮犰麸糸礤篝犴鲠祯濠┅